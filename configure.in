dnl Process this file with autoconf to produce a configure script

AC_PREREQ(2.57)

m4_define([flash_major_version], [0])
m4_define([flash_minor_version], [99])
m4_define([flash_micro_version],  [3])
m4_define([flash_version],
          [flash_major_version.flash_minor_version.flash_micro_version])
m4_define([flash_api_major], [1])
m4_define([flash_api_minor], [0])
m4_define([flash_api_version], [flash_api_major.flash_api_minor])

m4_define([gtk_required_version], [2.2.0])

AC_INIT([flash], [flash_version], [ljb@bitserf.org], [libflash])

AC_CONFIG_SRCDIR([flash/flash-file.c])
AC_CONFIG_AUX_DIR(config)

AM_INIT_AUTOMAKE(no-define)
AM_CONFIG_HEADER(config.h)

FLASH_MAJOR_VERSION=flash_major_version
FLASH_MINOR_VERSION=flash_minor_version
FLASH_MICRO_VERSION=flash_micro_version
FLASH_VERSION=flash_version
FLASH_API_MAJOR=flash_api_major
FLASH_API_MINOR=flash_api_minor
FLASH_API_VERSION=flash_api_version
AC_SUBST(FLASH_MAJOR_VERSION)
AC_SUBST(FLASH_MINOR_VERSION)
AC_SUBST(FLASH_MICRO_VERSION)
AC_SUBST(FLASH_VERSION)
AC_SUBST(FLASH_API_MAJOR)
AC_SUBST(FLASH_API_MINOR)
AC_SUBST(FLASH_API_VERSION)

AC_CANONICAL_HOST
AC_PROG_CC
AM_PROG_LIBTOOL

m4_define([debug_default], [no])

AC_ARG_ENABLE(debug,
              AC_HELP_STRING([--enable-debug=@<:@no/minimal/yes@:>@],
                             [enable debugging code @<:@default=debug_default@:>@]),,
              enable_debug=debug_default)

if test "x$enable_debug" = "xyes" ; then
  FLASH_DEBUG_FLAGS="-DFLASH_DEBUG_FULL"
else
  if test "x$enable_debug" = "xno" ; then
    FLASH_DEBUG_FLAGS=""
  else
    FLASH_DEBUG_FLAGS="-DFLASH_DEBUG_MINIMAL"
  fi
fi

AM_SANITY_CHECK

AC_ISC_POSIX
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

PKG_CHECK_MODULES(FLASH_LIB,
  [gtk+-2.0 >= gtk_required_version
	 gthread-2.0 >= gtk_required_version])

AS_COMPILER_FLAG(-Wall,
                 FLASH_ERROR_CFLAGS="-Wall",
                 FLASH_ERROR_CFLAGS="")

FLASH_LIB_CFLAGS="$FLASH_LIB_CFLAGS $FLASH_DEBUG_FLAGS $FLASH_ERROR_CFLAGS"
FLASH_LIB_OBJS="\$(top_builddir)/flash/libflash-$FLASH_API_VERSION.la"
AC_SUBST(FLASH_LIB_OBJS)

AC_HEADER_STDC
AC_C_CONST

# Check for X11
AC_PATH_XTRA
if test x$no_x = xyes ; then
  AC_MSG_ERROR([X development libraries not found])
fi

AC_CONFIG_FILES([
libflash-1.0.pc
Makefile
flash/flash-version.h
flash/Makefile
flash/sdk/Makefile
flash/sdk/obsolete/Makefile
])

AC_OUTPUT
